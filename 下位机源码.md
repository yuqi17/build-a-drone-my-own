按照作者的意思是将手机热点的wifi session id 和 密码都准备好，然后设置到如下代码中。最后再烧写；esp 芯片有 [AP(让别人连接它)和STA模式（去连别的热点）](https://blog.csdn.net/espressif/article/details/81742660)。
这里是一个 [arduino 上 AP 和 STA 的测试代码](https://blog.csdn.net/weixin_43232155/article/details/98237142)

#### [esp8266 pwm 原理](https://www.cnblogs.com/cuianbing/p/14409053.html) 
#### analogWriteRange(255) 限制了analogWrite(pin, value), value 为 0 - 255,默认是0 - 1023; 最大值占空比为 100%
#### pwm 的频率 analogWriteFreq(1000) 默认是1,000HZ; 作者建议为analogWriteFreq(5000)防止电机过热.
#### Arduino For ESP8266的PWM频率范围应该是100Hz-40,000Hz
### 硬件优化: 在电机两端并联 1微法的电容,让油门控制更平滑; [电机并联电容的作用](https://www.bilibili.com/video/av413628288/)

```c
//**************************************************
// WiFi Controlled Tiny Airplane
// ESP8266 Firmware ino file
// By Ravi Butani
// Rajkot INDIA
// Instructables page: https://www.instructables.com/id/WIFI-CONTROLLED-RC-PLANE/
//***************************************************
#include <ESP8266WiFi.h>
#include <WiFiUdp.h>

#define P_ID 1
#define ST_LED  2
#define L_MOTOR 5
#define R_MOTOR 4
#define DC_RSSI 1500  // Time in mS for send RSSI
#define DC_RX   900   // Time in mS for tx inactivity 200 old problem of motor stopping flickring
ADC_MODE(ADC_VCC);
unsigned int l_speed = 0;
unsigned int r_speed = 0;

unsigned long premillis_rssi = 0;
unsigned long premillis_rx   = 0;

int status = WL_IDLE_STATUS;
char ssid[] = "wifiplane";   //  your network SSID (name)
char pass[] = "wifiplane1234";    // your network password (use for WPA, or use as key for WEP)
int keyIndex = 0;            // your network key Index number (needed only for WEP)
IPAddress remotIp;
unsigned int localPort = 6000;      // local port to listen on
unsigned int remotPort = 2390;      // local port to talk on
char  packetBuffer[10]; //buffer to hold incoming packet
char  replyBuffer[]={P_ID,0x01,0x01,0x00}; // a string to send back
WiFiUDP Udp;

// the setup function runs once when you press reset or power the board
void setup() {
  WiFi.mode(WIFI_STA);
  //WiFi.setOutputPower(2.5);
  analogWriteRange(255);
  pinMode(L_MOTOR, OUTPUT);
  pinMode(R_MOTOR, OUTPUT);
  analogWrite(L_MOTOR,0);
  analogWrite(R_MOTOR,0);
  pinMode(ST_LED, OUTPUT);
  digitalWrite(ST_LED,HIGH);
  //Serial.begin(115200);
  WiFi.begin(ssid, pass);
  while (WiFi.status() != WL_CONNECTED) 
  {
    digitalWrite(ST_LED,LOW);
    delay(60);
    digitalWrite(ST_LED,HIGH);
    delay(1000);
    //Serial.print(".");
  }
  remotIp=WiFi.localIP();
  remotIp[3] = 255;
  Udp.begin(localPort);
}

// the loop function runs over and over again forever
void loop() {
  delay(5);
  if(WiFi.status() == WL_CONNECTED)
  {
    digitalWrite(ST_LED,LOW);
    // if there's data available, read a packet
    int packetSize = Udp.parsePacket();
    if (packetSize) 
    {
      // read the packet into packetBufffer
      int len = Udp.read(packetBuffer, 10);
      if (len > 2) 
      {
        if(packetBuffer[0] == P_ID)
        {
          l_speed = (unsigned int)packetBuffer[1]*2-2;
          r_speed = (unsigned int)packetBuffer[2]*2-2;
          //Serial.print(l_speed);
          //Serial.print(" \t");
          //Serial.println(r_speed);
          analogWrite(L_MOTOR,l_speed);
          analogWrite(R_MOTOR,r_speed);
          premillis_rx = millis();
        }
      }
      
    }
    if(millis()-premillis_rssi > DC_RSSI)
    {
       premillis_rssi = millis();
       long rssi = abs(WiFi.RSSI());
       float vcc = (((float)ESP.getVcc()/(float)1024.0)+0.75f)*10;
       replyBuffer[1] = (unsigned char)rssi;
       replyBuffer[2] = (unsigned char)vcc;
       
       Udp.beginPacket(remotIp, remotPort);
       Udp.write(replyBuffer);
       Udp.endPacket();
     }
     if(millis()-premillis_rx > DC_RX)
     {
       analogWrite(L_MOTOR,0);
       analogWrite(R_MOTOR,0);
       //Serial.println("nodata");
     }
  }
  else
  {
    digitalWrite(ST_LED,LOW);
    delay(60);
    digitalWrite(ST_LED,HIGH);
    delay(1000);
    analogWrite(L_MOTOR,0);
    analogWrite(R_MOTOR,0);
    digitalWrite(ST_LED,HIGH);
  }
}

```
